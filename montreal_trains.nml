//set depot vehicle width to 32px to avoid overlap
train_width_32_px = 1;
//align train properly in depot window
traininfo_y_offset = 2;

grf {
        grfid: "JTM\03";
        name: string(STR_GRF_NAME);
        desc: string(STR_GRF_DESCRIPTION);
        version: 1;
        min_compatible_version: 1;
}

///////////////
// TEMPLATES //
///////////////

template template_purchase(x, y) {
    //[left_x, upper_y, width, height, offset_x, offset_y]
    [x,         y,          71,         13,         -35,        -8]
}

template template_32x15_8(x, y) {
    //[left_x, upper_y, width, height, offset_x, offset_y]
    [x,         y,          8,          22,         -3,         -13]
    [x + 9,     y,          21,         18,         -13,        -10]
    [x + 31,    y,          32,         15,         -14,        -11]
    [x + 64,    y,          21,         18,         -9,         -12]
    [x + 86,    y,          8,          22,         -3,         -13]
    [x + 95,    y,          21,         18,         -13,        -10]
    [x + 117,   y,          32,         15,         -14,        -11]
    [x + 150,   y,          21,         18,         -8,         -11]
}

template template_32x15_8_reverse(x, y) {
    //[left_x, upper_y, width, height, offset_x, offset_y]
    [x + 86,    y,          8,          22,         -3,         -13]
    [x + 95,    y,          21,         18,         -13,        -10]
    [x + 117,   y,          32,         15,         -14,        -11]
    [x + 150,   y,          21,         18,         -9,         -12]
    [x,         y,          8,          22,         -3,         -13]
    [x + 9,     y,          21,         18,         -13,        -10]
    [x + 31,    y,          32,         15,         -14,        -11]
    [x + 64,    y,          21,         18,         -8,         -11]
}

template template_32x15_5(x, y) {
    //[left_x, upper_y, width, height, offset_x, offset_y]
    [x,         y + 5,      8,          17,         -3,         -8]
    [x + 9,     y + 4,      15,         14,         -16,        -5]
    [x + 31,    y + 2,      20,         12,         -16,        -8]
    [x + 64,    y + 1,      15,         14,         -5,         -9]
    [x + 86,    y,          8,          17,         -3,         -8]
    [x + 101,   y + 1,      15,         14,         -16,        -5]
    [x + 129,   y + 2,      20,         12,         -16,        -8]
    [x + 156,   y + 4,      15,         14,         -5,         -9]
}

template template_32x15_5_reverse(x, y) {
    //[left_x, upper_y, width, height, offset_x, offset_y]
    [x + 86,    y,          8,          17,         -3,         -8]
    [x + 101,   y + 1,      15,         14,         -16,        -5]
    [x + 129,   y + 2,      20,         12,         -15,        -8]
    [x + 156,   y + 4,      15,         14,         -5,         -9]
    [x,         y + 5,      8,          17,         -3,         -8]
    [x + 9,     y + 4,      15,         14,         -16,        -5]
    [x + 31,    y + 2,      20,         12,         -17,        -8]
    [x + 64,    y + 1,      15,         14,         -5,         -9]
}

////////////////////
// RAILTYPE TABLE //
////////////////////

railtypetable {
    ELRL
}

////////////////
// SPRITESETS //
////////////////

// 100 Series

spriteset (spriteset_purchase_STM_MR_63_3, "gfx/trains/spritesheet.png") {
	template_purchase(689, 7)
}

spriteset (spriteset_STM_MR_63_middle, "gfx/trains/spritesheet.png") {
	template_32x15_5(1, 7)
}

spriteset (spriteset_STM_MR_63_original_front_lights, "gfx/trains/spritesheet.png") {
	template_32x15_5(173, 7)
}

spriteset (spriteset_STM_MR_63_refurbished_front_lights, "gfx/trains/spritesheet.png") {
	template_32x15_5(173, 36)
}

spriteset (spriteset_STM_MR_63_original_front_nolights, "gfx/trains/spritesheet.png") {
	template_32x15_5(345, 7)
}

spriteset (spriteset_STM_MR_63_refurbished_front_nolights, "gfx/trains/spritesheet.png") {
	template_32x15_5(345, 36)
}

spriteset (spriteset_STM_MR_63_original_rear_lights, "gfx/trains/spritesheet.png") {
	template_32x15_5_reverse(517, 7)
}

spriteset (spriteset_STM_MR_63_refurbished_rear_lights, "gfx/trains/spritesheet.png") {
	template_32x15_5_reverse(517, 36)
}

spriteset (spriteset_STM_MR_63_original_rear_nolights, "gfx/trains/spritesheet.png") {
	template_32x15_5_reverse(345, 7)
}

spriteset (spriteset_STM_MR_63_refurbished_rear_nolights, "gfx/trains/spritesheet.png") {
	template_32x15_5_reverse(345, 36)
}

//////////////
// SWITCHES //
//////////////

// STM MR-63

switch (FEAT_TRAINS, SELF, switch_spriteset_STM_MR_63_front_lights, cargo_subtype == 0) {
    1: return spriteset_STM_MR_63_original_front_lights;
    return spriteset_STM_MR_63_refurbished_front_lights;
}

switch (FEAT_TRAINS, SELF, switch_spriteset_STM_MR_63_front_nolights, cargo_subtype == 0) {
    1: return spriteset_STM_MR_63_original_front_nolights;
    return spriteset_STM_MR_63_refurbished_front_nolights;
}

switch (FEAT_TRAINS, SELF, switch_spriteset_STM_MR_63_front, position_in_consist) {
    0: return switch_spriteset_STM_MR_63_front_lights;
    return switch_spriteset_STM_MR_63_front_nolights; // only the vehicle at the front is lit up
}

switch (FEAT_TRAINS, SELF, switch_spriteset_STM_MR_63_rear_lights, cargo_subtype == 0) {
    1: return spriteset_STM_MR_63_original_rear_lights;
    return spriteset_STM_MR_63_refurbished_rear_lights;
}

switch (FEAT_TRAINS, SELF, switch_spriteset_STM_MR_63_rear_nolights, cargo_subtype == 0) {
    1: return spriteset_STM_MR_63_original_rear_nolights;
    return spriteset_STM_MR_63_refurbished_rear_nolights;
}

switch (FEAT_TRAINS, SELF, switch_spriteset_STM_MR_63_rear, position_in_consist_from_end) {
    0: return switch_spriteset_STM_MR_63_rear_lights;
    return switch_spriteset_STM_MR_63_rear_nolights; // only the vehicle at the rear is lit up
}

switch (FEAT_TRAINS, SELF, switch_articulated_STM_MR_63_3, extra_callback_info1) {
    1..2: return item_STM_MR_63_3;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS; //stop adding vehicle parts
}

switch (FEAT_TRAINS, SELF, switch_spriteset_STM_MR_63_3, position_in_vehid_chain % 3) {
    0: switch_spriteset_STM_MR_63_front;
    1: spriteset_STM_MR_63_middle;
    2: switch_spriteset_STM_MR_63_rear;
}

switch (FEAT_TRAINS, SELF, switch_STM_MR_63_cargo_subtype_text, cargo_subtype) {
    0: return string(STR_STM_MR_63_SUBTYPE_ORIGINAL);
    1: return string(STR_STM_MR_63_SUBTYPE_REFURBISHED);
    return CB_RESULT_NO_TEXT;
}

/////////////////
// ITEM BLOCKS //
/////////////////

/* Define the MR-63 Series (3 Car) */
item(FEAT_TRAINS, item_STM_MR_63_3) {
    property {
        /* Properties common to all vehicle types */
        name:                           string(STR_NAME_STM_MR_63_3);
        climates_available:             bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
        introduction_date:              date(1966,10,14);
        model_life:                     30;
        /* retire_early not set, use default retirement behaviour */
        vehicle_life:                   55;
        reliability_decay:              20;
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   bitmask(CC_MAIL, CC_EXPRESS, CC_ARMOURED, CC_BULK, CC_PIECE_GOODS, CC_LIQUID, CC_REFRIGERATED, CC_HAZARDOUS, CC_COVERED, CC_OVERSIZED, CC_POWDERIZED, CC_NON_POURABLE, CC_NEO_BULK);
        loading_speed:                  60;
        cost_factor:                    20;
        running_cost_factor:            20;

        /* Train-specific properties */
        sprite_id:                      SPRITE_ID_NEW_TRAIN; //enable new graphics
        speed:                          80 km/h;
        misc_flags:                     bitmask(TRAIN_FLAG_2CC, TRAIN_FLAG_MU);
        refit_cost:                     0; // Refitting is free
        /* callback_flags are not set manually */
        track_type:                     ELRL;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        power:                          125 * 4 kW;
        weight:                         20 + 26 * 2 ton;
        cargo_capacity:                 109;
        length:                         5;
        tractive_effort_coefficient:    0.72;
        air_drag_coefficient:           0.06;
        visual_effect_and_powered:      visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 0, ENABLE_WAGON_POWER);
    }

    /* Define graphics */
    graphics {
        purchase:                   spriteset_purchase_STM_MR_63_3;
        cargo_subtype_text:         switch_STM_MR_63_cargo_subtype_text;
        articulated_part:           switch_articulated_STM_MR_63_3;
        additional_text:            return string(STR_PURCHASE_STM_MR_63_3);
        cargo_capacity:             109;
        purchase_cargo_capacity:    spriteset_purchase_STM_MR_63_3;
        default:                    switch_spriteset_STM_MR_63_3;
    }
}